    INITIAL X$TM,15000
    
    ;Интервалы времени поступления заявок в систему
    INITIAL X$T1_,90
    INITIAL X$T1DISP_,10
    INITIAL X$N1_,30
    INITIAL X$T2_,250
    INITIAL X$T3_,120
    INITIAL x$T3DISP_,35
    INITIAL X$N3_,45

    ;Время обработок заявок на терминале
    INITIAL X$TERM1_,30
    INITIAL X$TERM1DISP_,10
    INITIAL X$TERM2_,40
    INITIAL X$TERM2DISP_,15
    INITIAL X$TERM3_,35
    INITIAL X$TERM3DISP_,12

    ;Время обработки на ЦВК
    INITIAL X$TCVK_,50
    INITIAL X$TCVKDISP_,5

    INITIAL X$P1_,.7    ;Вероятность перехода на терминал

    INITIAL X$QISOC_,0  ;
    INITIAL X$TERM1NUM,0;Номер заявки поступившей с ЦВК на терминал1
    INITIAL X$TERM1MAX,5;
    INITIAL X$TERM2NUM,0;Номер заявки поступившей с ЦВК на терминал2
    INITIAL X$TERM2MAX,4;
    INITIAL X$TERM3NUM,0;Номер заявки поступившей с ЦВК на терминал2
    INITIAL X$TERM3MAX,3;

    INITIAL X$PROC,0;Количество обработанных заявок, обработанных на ЦВК на момент, когда систему покинет первая заявка
    INITIAL X$FLAG,0;

ARMPRIOR FUNCTION P1,E3         ;Определяем приоритет заявки
1,3/2,2/3,2

ARMTIME FUNCTION RN1,D3         ;Время обработки на АРМ
.5,18/.8,25/1,30

KMMTIME FUNCTION P1,E3          ;Время обработки на КММ
1,2/2,FN$KMM2TIME/3,1

KMM2TIME FUNCTION RN1,D2        ;Время обработки заявки с АРМ2 на КММ
.2,3/1,1


 
        GENERATE X$T1_,X$T1DISP_,,X$N1_       ;Генерация транзактов для АРМ1
        ASSIGN 1,1                      	  ;Первый параметр - номер терминала
MTERM1  QUEUE TERM1                   	  
        SEIZE TERM1
        DEPART TERM1
        ADVANCE X$TERM1_,X$TERM1DISP_
        RELEASE TERM1

MARM1   QUEUE ARM1                      ;Обработка на АРМ1
        SEIZE ARM1
        DEPART ARM1
        PRIORITY FN$ARMPRIOR            ;Задаём заявке приоритет
        ADVANCE FN$ARMTIME
        RELEASE ARM1
        ASSIGN 2,0                      ;Второй параметр - была ли заявка на ЦВК
        TRANSFER X$P1_,MKMM,MTERM1

        GENERATE X$T2_
        ASSIGN 1,2
MTERM2  QUEUE TERM2
        SEIZE TERM2
        DEPART TERM2
        ADVANCE X$TERM2_,X$TERM2DISP_
        RELEASE TERM2

MARM2   QUEUE ARM2
        SEIZE ARM2
        DEPART ARM2
        PRIORITY FN$ARMPRIOR
        ADVANCE FN$ARMTIME
        RELEASE ARM2
        ASSIGN 2,0
        TRANSFER X$P1_,MKMM,MTERM2

        GENERATE X$T3_,X$T3DISP_,,X$N3_
MTERM3  ASSIGN 1,3
        QUEUE TERM3
        SEIZE TERM3
        DEPART TERM3
        ADVANCE X$TERM3_,X$TERM3DISP_
        RELEASE TERM3

MARM3   QUEUE ARM3
        SEIZE ARM3
        DEPART ARM3
        PRIORITY FN$ARMPRIOR
        ADVANCE FN$ARMTIME
        RELEASE ARM3
        ASSIGN 2,0
        TRANSFER X$P1_,MKMM,MTERM3



MKMM    TEST E X$QISOC_,1,M2KMM      ;Если КММ занят, то добавляем в очередь
        LINK QKMM,PR	;по приоритету

M2KMM   SAVEVALUE QISOC_,1
        QUEUE KMM
        SEIZE KMM
        DEPART KMM
        ADVANCE FN$KMMTIME
        RELEASE KMM

        TEST NE CH$QKMM,0,MZERO         ;Если в очереди есть транзакт, извлекаем его
        UNLINK QKMM,M2KMM,1
        TRANSFER ,MDISTR

MZERO   SAVEVALUE QISOC_,0               ;Если КММ не занята, то QISOC_ = 0

MDISTR  TEST E P2,1,MCVK                 ;Если транзакт не был на ЦВК, отправляем его туда
        TEST E P1,1,TOTERM2              ;Если транзакт с первого терминала, то отправляем его обратно
TOTERM1 SAVEVALUE TERM1NUM+,1            
        TEST E X$TERM1NUM,X$TERM1MAX,MTERM1 ;Если это 5-я заявка с ЦВК, то уничтожаем её
        SAVEVALUE TERM1NUM,0            	;Обнуляем счётчик заявок
        TRANSFER ,MKILL 
TOTERM2 TEST E P1,2,TOTERM3
        TEST E X$TERM2NUM,X$TERM2MAX,MTERM2 ;Если это 4-я заявка с ЦВК, то уничтожаем её
        SAVEVALUE TERM2NUM,0            	;Обнуляем счётчик заявок
        TRANSFER ,MKILL 
TOTERM3 TEST E X$TERM3NUM,X$TERM3MAX,MTERM3 ;Если это 3-я заявка с ЦВК, то уничтожаем её
        SAVEVALUE TERM3NUM,0            	;Обнуляем счётчик заявок
        TRANSFER ,MKILL 

MCVK    PRIORITY 1
        QUEUE CVK
        SEIZE CVK
        DEPART CVK
        ADVANCE X$TCVK_,X$TCVKDISP_
        RELEASE CVK
        TEST E X$FLAG,0,FLAGUP
        SAVEVALUE PROC+,1;
FLAGUP  ASSIGN 2,1                      ;Транзакт прошел ЦВК
        TRANSFER ,MKMM

MKILL   SAVEVALUE FLAG,1
        TERMINATE

        GENERATE X$TM
        TERMINATE 1
        START 1